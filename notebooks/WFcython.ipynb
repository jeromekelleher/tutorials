{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# A Wright-Fisher simulation implemented in C via Cython.\n",
    "\n",
    "OMG!\n",
    "\n",
    "We would use GSL via CythonGSL, but that would require a GPL license for this notebook, but we're doing CCBY."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 1,
   "metadata": {},
   "outputs": [],
   "source": [
    "%load_ext Cython"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 2,
   "metadata": {},
   "outputs": [
    {
     "name": "stderr",
     "output_type": "stream",
     "text": [
      "/Users/kevin/anaconda3/lib/python3.5/site-packages/h5py/__init__.py:36: FutureWarning: Conversion of the second argument of issubdtype from `float` to `np.floating` is deprecated. In future, it will be treated as `np.float64 == np.dtype(float).type`.\n",
      "  from ._conv import register_converters as _register_converters\n"
     ]
    }
   ],
   "source": [
    "import msprime\n",
    "import numpy as np"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 3,
   "metadata": {},
   "outputs": [],
   "source": [
    "%%cython\n",
    "\n",
    "import msprime\n",
    "import numpy as np\n",
    "cimport numpy as np\n",
    "from cython.view cimport array as cvarray\n",
    "from libc.stdlib cimport malloc, realloc, free\n",
    "from libc.stdint cimport int32_t, uint32_t\n",
    "\n",
    "cdef int32_t * malloc_int32_t(size_t n):\n",
    "    return <int32_t*>malloc(n*sizeof(int32_t))\n",
    "\n",
    "cdef int32_t * realloc_int32_t(void * x, size_t n):\n",
    "    return <int32_t*>realloc(x,n*sizeof(int32_t))\n",
    "\n",
    "cdef double * malloc_double(size_t n):\n",
    "    return <double*>malloc(n*sizeof(double))\n",
    "\n",
    "cdef double * realloc_double(double * x, size_t n):\n",
    "    return <double*>realloc(<double *>x,n*sizeof(double))\n",
    "\n",
    "cdef struct Mutations:\n",
    "    double * pos\n",
    "    int32_t * time\n",
    "    size_t next_mutation, capacity\n",
    "    \n",
    "cdef int init_Mutations(Mutations * m):\n",
    "    m.next_mutation = 0\n",
    "    m.capacity = 10000\n",
    "    m.pos = malloc_double(m.capacity)\n",
    "    if m.pos == NULL:\n",
    "        return -1\n",
    "    m.time = malloc_int32_t(m.capacity)\n",
    "    if m.time == NULL:\n",
    "        return -1\n",
    "    return 0\n",
    "\n",
    "cdef int realloc_Mutations(Mutations * m):\n",
    "    m.capacity *= 2\n",
    "    m.pos = realloc_double(m.pos,\n",
    "                          m.capacity)\n",
    "    if m.pos == NULL:\n",
    "        return -1\n",
    "    m.time = realloc_int32_t(m.time,\n",
    "                            m.capacity)\n",
    "    if m.time == NULL:\n",
    "        return -1\n",
    "    return 0\n",
    "\n",
    "cdef void free_Mutations(Mutations * m):\n",
    "    free(m.pos)\n",
    "    free(m.time)\n",
    "    m.next_mutation = 0\n",
    "    m.capacity = 10000\n",
    "    \n",
    "cdef int add_mutation(double pos,\n",
    "                     int32_t generation,\n",
    "                     Mutations * m):\n",
    "    cdef int rv = 0\n",
    "    if m.next_mutation+1 >= m.capacity:\n",
    "        rv = realloc_Mutations(m)\n",
    "        if rv != 0:\n",
    "            return rv\n",
    "    m.pos[m.next_mutation] = pos\n",
    "    m.time[m.next_mutation] = generation\n",
    "    m.next_mutation+=1\n",
    "    return rv\n",
    "    \n",
    "cdef struct Nodes:\n",
    "    double * time\n",
    "    size_t next_node, capacity\n",
    "    \n",
    "cdef int init_Nodes(Nodes * n):\n",
    "    n.next_node = 0\n",
    "    n.capacity = 10000\n",
    "    n.time = malloc_double(n.capacity)\n",
    "    if n.time == NULL:\n",
    "        return -1\n",
    "    return 0\n",
    "\n",
    "cdef int realloc_Nodes(Nodes * n):\n",
    "    n.capacity *= 2\n",
    "    n.time = realloc_double(n.time,\n",
    "                            n.capacity)\n",
    "    if n.time == NULL:\n",
    "        return -1\n",
    "    return 0\n",
    "    \n",
    "cdef void free_Nodes(Nodes * n):\n",
    "    if n.time != NULL:\n",
    "        free(n.time)\n",
    "    n.next_node = 0\n",
    "    n.capacity = 10000\n",
    "\n",
    "cdef int add_node(double t, Nodes *n):\n",
    "    cdef int rv = 0\n",
    "    if n.next_node >= n.capacity:\n",
    "        rv = realloc_Nodes(n)\n",
    "        if rv != 0:\n",
    "            return rv\n",
    "    n.time[n.next_node] = t\n",
    "    n.next_node+=1\n",
    "    return rv\n",
    "    \n",
    "cdef struct Edges:\n",
    "    double *left\n",
    "    double *right\n",
    "    int32_t *parent\n",
    "    int32_t *child\n",
    "    size_t next_edge, capacity\n",
    "    \n",
    "cdef int init_Edges(Edges * e):\n",
    "    e.next_edge = 0\n",
    "    e.capacity = 10000\n",
    "    e.left = malloc_double(e.capacity)\n",
    "    if e.left == NULL:\n",
    "        return -1\n",
    "    e.right = malloc_double(e.capacity)\n",
    "    if e.right == NULL:\n",
    "        return -1\n",
    "    e.parent = malloc_int32_t(e.capacity)\n",
    "    if e.parent == NULL:\n",
    "        return -1\n",
    "    e.child = malloc_int32_t(e.capacity)\n",
    "    if e.child == NULL:\n",
    "        return -1\n",
    "    return 0\n",
    "   \n",
    "cdef int realloc_Edges(Edges * e):\n",
    "    e.capacity *= 2\n",
    "    e.left = realloc_double(e.left,e.capacity)\n",
    "    if e.left == NULL:\n",
    "        return -1\n",
    "    e.right = realloc_double(e.right,e.capacity)\n",
    "    if e.right == NULL:\n",
    "        return -1\n",
    "    e.parent = realloc_int32_t(e.parent,e.capacity)\n",
    "    if e.parent == NULL:\n",
    "        return -1\n",
    "    e.child = realloc_int32_t(e.child,e.capacity)\n",
    "    if e.child == NULL:\n",
    "        return -1\n",
    "    return 0\n",
    "\n",
    "cdef void free_Edges(Edges * e):\n",
    "    free(e.left)\n",
    "    free(e.right)\n",
    "    free(e.parent)\n",
    "    free(e.child)\n",
    "    e.next_edge = 0\n",
    "    e.capacity = 10000\n",
    "    \n",
    "cdef int add_edge(double left, double right,\n",
    "             int32_t parent, int32_t child,\n",
    "             Edges * edges):\n",
    "    cdef int rv=0\n",
    "    if edges.next_edge+1 >= edges.capacity:\n",
    "        rv = realloc_Edges(edges)\n",
    "        if rv != 0:\n",
    "            return rv\n",
    "        \n",
    "    edges.left[edges.next_edge] = left\n",
    "    edges.right[edges.next_edge] = right\n",
    "    edges.parent[edges.next_edge] = parent\n",
    "    edges.child[edges.next_edge] = child\n",
    "    edges.next_edge += 1\n",
    "    return rv\n",
    "\n",
    "cdef void cleanup(Nodes * n, Edges * e, Mutations * m):\n",
    "    free_Nodes(n)\n",
    "    free_Edges(e)\n",
    "    free_Mutations(m)\n",
    "    \n",
    "cdef int infsites(double mu, int32_t generation,\n",
    "                  Mutations * mutations,\n",
    "                  dict lookup):\n",
    "    cdef unsigned nmut = np.random.poisson(mu)\n",
    "    cdef unsigned i = 0\n",
    "    cdef np.ndarray[double,ndim=1] pos\n",
    "    cdef int rv = 0\n",
    "    for i in range(nmut):\n",
    "        pos = np.random.random_sample(1)\n",
    "        while pos[0] in lookup:\n",
    "            pos = np.random.random_sample(1)\n",
    "        rv = add_mutation(pos[0],\n",
    "                         generation,\n",
    "                         mutations)\n",
    "        if rv != 0:\n",
    "            return rv\n",
    "        lookup[pos[0]] = True\n",
    "    return rv\n",
    "\n",
    "cdef int poisson_recombination(double r,\n",
    "                                tuple parent_indexes,\n",
    "                                int32_t next_offspring_id,\n",
    "                                Edges * edges):\n",
    "    cdef unsigned nbreaks = np.random.poisson(r)\n",
    "    cdef list b = []\n",
    "    cdef unsigned i = 0\n",
    "    cdef np.ndarray[double,ndim=1] x\n",
    "    cdef list pgams\n",
    "    cdef int rv = 0\n",
    "    cdef double left,right\n",
    "    cdef int32_t p\n",
    "    if nbreaks == 0:\n",
    "        # The parent passes the entire region onto the child\n",
    "        rv = add_edge(0.0,1.0,parent_indexes[0],\n",
    "                      next_offspring_id,edges)\n",
    "        if rv != 0:\n",
    "            return rv\n",
    "    else:\n",
    "        while i < nbreaks:\n",
    "            x = np.random.random_sample(1)\n",
    "            while x[0] in b:\n",
    "                x = np.random.random_sample(1)\n",
    "            b.append(x[0])\n",
    "            i += 1\n",
    "        b.sort()\n",
    "        b.append(1.0)\n",
    "\n",
    "        if b[0] != 0.0:\n",
    "            b.insert(0,0.0)\n",
    "        else:\n",
    "            parent_indexes = (parent_indexes[1], parent_indexes[0])\n",
    "        \n",
    "        pgams = list([*tuple(i for i in parent_indexes)]*int(len(b)/2))\n",
    "\n",
    "        for left,right,p in zip(b[0:len(b)-1],b[1:len(b)],pgams):\n",
    "            rv = add_edge(left,right,p,\n",
    "                          next_offspring_id,edges)\n",
    "            if rv != 0:\n",
    "                return rv\n",
    "    return 0\n",
    "\n",
    "def evolve(int N, int ngens, double theta, double rho, int gc):\n",
    "    nodes = msprime.NodeTable()\n",
    "    edges = msprime.EdgeTable()\n",
    "    mutations = msprime.MutationTable()\n",
    "    \n",
    "    cdef double mu = theta/<double>(4*N)\n",
    "    cdef double r = rho/<double>(4*N)\n",
    "    \n",
    "    cdef int rv\n",
    "    cdef size_t i, generation\n",
    "    cdef Nodes temp_nodes\n",
    "    cdef Edges temp_edges\n",
    "    cdef Mutations temp_mutations\n",
    "    rv = init_Nodes(&temp_nodes)\n",
    "    if rv != 0:\n",
    "        cleanup(&temp_nodes,&temp_edges,&temp_mutations)\n",
    "        raise RuntimeError(\"could not initialize temp_nodes\")\n",
    "    rv = init_Edges(&temp_edges)\n",
    "    if rv != 0:\n",
    "        cleanup(&temp_nodes,&temp_edges,&temp_mutations)\n",
    "        raise RuntimeError(\"could not initialize temp_edges\")\n",
    "    rv = init_Mutations(&temp_mutations)\n",
    "    if rv != 0:\n",
    "        cleanup(&temp_nodes,&temp_edges,&temp_mutations)\n",
    "        raise RuntimeError(\"could not initialize temp_mutations\")\n",
    "        \n",
    "    for i in range(2*<size_t>N):\n",
    "        nodes.add_row(time=0.0,\n",
    "                      flags=msprime.NODE_IS_SAMPLE)\n",
    "        \n",
    "    \n",
    "    cdef int32_t next_offspring_index, first_parental_index\n",
    "    next_offspring_index = len(nodes)\n",
    "    first_parental_index = 0\n",
    "    PARENT_DTYPE = np.int32\n",
    "    cdef np.ndarray[int32_t,ndim=1] parents\n",
    "    cdef double mendel[2]\n",
    "    cdef size_t parent1, parent2,pindex\n",
    "    cdef int32_t[:] pview\n",
    "    cdef int32_t p1g1, p1g2, p2g1, p2g2\n",
    "    cdef dict lookup = {}\n",
    "    for generation in range(1,<size_t>(ngens+1)):\n",
    "        parents = np.random.randint(0, N, 2*N, dtype=PARENT_DTYPE)\n",
    "        pview = parents\n",
    "        for pindex in range(0,2*N,2):\n",
    "            parent1=pview[pindex]\n",
    "            parent2=pview[pindex+1]\n",
    "            p1g1 = first_parental_index + 2*parent1\n",
    "            p1g2 = p1g1 + 1\n",
    "            p2g1 = first_parental_index + 2*parent2\n",
    "            p2g2 = p2g1 + 1\n",
    "            \n",
    "            mendel = np.random.random_sample(2)\n",
    "            if mendel[0] < 0.5:\n",
    "                p1g1, p1g2 = p1g2, p1g1\n",
    "            if mendel[1] < 0.5:\n",
    "                p2g1, p2g2 = p2g2, p2g1\n",
    "                \n",
    "            rv = poisson_recombination(r,(p1g1,p1g2),\n",
    "                                      next_offspring_index,\n",
    "                                      &temp_edges)\n",
    "            if rv != 0:\n",
    "                cleanup(&temp_nodes,&temp_edges,\n",
    "                       &temp_mutations)\n",
    "                raise RuntimeError(\"error during recombination\")\n",
    "                \n",
    "            rv = infsites(mu,generation,\n",
    "                         &temp_mutations,lookup)\n",
    "            \n",
    "            if rv != 0:\n",
    "                cleanup(&temp_nodes,&temp_edges,\n",
    "                       &temp_mutations)\n",
    "                raise RuntimeError(\"error during mutation\")\n",
    "                \n",
    "            rv = add_node(<double>generation, &temp_nodes)\n",
    "            if rv != 0:\n",
    "                cleanup(&temp_nodes,&temp_edges,\n",
    "                       &temp_mutations)\n",
    "                raise RuntimeError(\"error during adding nodes\")\n",
    "                \n",
    "            next_offspring_index += 1\n",
    "                \n",
    "            rv = poisson_recombination(r,(p2g1,p2g2),\n",
    "                                      next_offspring_index,\n",
    "                                      &temp_edges)\n",
    "            if rv != 0:\n",
    "                cleanup(&temp_nodes,&temp_edges,\n",
    "                       &temp_mutations)\n",
    "                raise RuntimeError(\"error during recombination\")\n",
    "                \n",
    "            rv = infsites(mu,generation,\n",
    "                         &temp_mutations,lookup)\n",
    "            \n",
    "            if rv != 0:\n",
    "                cleanup(&temp_nodes,&temp_edges,\n",
    "                       &temp_mutations)\n",
    "                raise RuntimeError(\"error during mutation\")\n",
    "                \n",
    "            rv = add_node(<double>generation, &temp_nodes)\n",
    "            if rv != 0:\n",
    "                cleanup(&temp_nodes,&temp_edges,\n",
    "                       &temp_mutations)\n",
    "                raise RuntimeError(\"error during adding nodes\")\n",
    "                \n",
    "            next_offspring_index += 1\n",
    "        first_parental_index += 2*N\n",
    "    \n",
    "    # Push first nodes times further back\n",
    "    nodes.set_columns(time=nodes.time + ngens +1,\n",
    "                     flags=nodes.flags)\n",
    "            \n",
    "    # Add our data to the tables\n",
    "    cdef double[:] timeview = <double[:temp_nodes.next_node]>temp_nodes.time\n",
    "    time=np.asarray(timeview,np.float)\n",
    "    time-=time.max()\n",
    "    time*=-1.0\n",
    "    nodes.append_columns(time=time,\n",
    "                         flags=np.ones(temp_nodes.next_node,np.uint32))\n",
    "    edges.append_columns(left=np.asarray(<double[:temp_edges.next_edge]>temp_edges.left),\n",
    "                        right=np.asarray(<double[:temp_edges.next_edge]>temp_edges.right),\n",
    "                        parent=np.asarray(<int32_t[:temp_edges.next_edge]>temp_edges.parent),\n",
    "                        child=np.asarray(<int32_t[:temp_edges.next_edge]>temp_edges.child))\n",
    "    \n",
    "    print(nodes.time.min(),nodes.time.max())\n",
    "\n",
    "    msprime.sort_tables(nodes=nodes,edges=edges)\n",
    "    \n",
    "    samples = np.where(nodes.time==0)[0]\n",
    "    \n",
    "    print(samples)\n",
    "    \n",
    "    msprime.simplify_tables(samples=samples.tolist(),\n",
    "                           nodes=nodes,\n",
    "                           edges=edges)\n",
    "                \n",
    "    cleanup(&temp_nodes,&temp_edges,&temp_mutations)\n",
    "    \n",
    "    return msprime.load_tables(nodes=nodes,edges=edges)\n",
    "    \n",
    "    \n",
    "def test_infsites():\n",
    "    cdef Mutations m\n",
    "    init_Mutations(&m)\n",
    "    cdef dict lookup = {}\n",
    "    cdef int rv = infsites(100000,1,&m,lookup)\n",
    "    print(m.next_mutation,m.capacity)\n",
    "    free_Mutations(&m)\n",
    "    print(m.next_mutation,m.capacity)\n",
    "    print(\"done!\")\n",
    "    \n",
    "def test_add_edges():\n",
    "    cdef Edges e\n",
    "    cdef int rv = init_Edges(&e)\n",
    "    print(e.next_edge,e.capacity)\n",
    "    print(e.left == NULL)\n",
    "    print(e.right == NULL)\n",
    "    print(e.parent == NULL)\n",
    "    print(e.child == NULL)\n",
    "    print(rv)\n",
    "    for i in range(20000):\n",
    "        rv=add_edge(0,1,0,1,&e)\n",
    "        #print(e.next_edge,e.capacity)\n",
    "        # print(i,rv)\n",
    "        if rv != 0:\n",
    "            raise RuntimeError(\"error adding edges\")\n",
    "    print(e.next_edge,e.capacity)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 8,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "-0.0 10001.0\n",
      "[20000000 20000001 20000002 ... 20001997 20001998 20001999]\n",
      "CPU times: user 1min 7s, sys: 2.14 s, total: 1min 9s\n",
      "Wall time: 1min 9s\n"
     ]
    },
    {
     "data": {
      "text/plain": [
       "<msprime.trees.TreeSequence at 0x1189eae48>"
      ]
     },
     "execution_count": 8,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "%%time\n",
    "evolve(1000, 10000, 100.0, 100.0, 10)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 5,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "99794 160000\n",
      "0 10000\n",
      "done!\n"
     ]
    }
   ],
   "source": [
    "test_infsites()"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 6,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "0 10000\n",
      "False\n",
      "False\n",
      "False\n",
      "False\n",
      "0\n",
      "20000 40000\n"
     ]
    }
   ],
   "source": [
    "test_add_edges()"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Python [conda root]",
   "language": "python",
   "name": "conda-root-py"
  },
  "language_info": {
   "codemirror_mode": {
    "name": "ipython",
    "version": 3
   },
   "file_extension": ".py",
   "mimetype": "text/x-python",
   "name": "python",
   "nbconvert_exporter": "python",
   "pygments_lexer": "ipython3",
   "version": "3.5.4"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 2
}
